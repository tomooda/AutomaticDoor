module AutomaticDoor
imports
    from Sensor all,
    from Motor all
exports all
definitions
types
    Time = <ZERO> | <ONE> | <TWO>;
values
    OPEN_DURATION : Time = <TWO>;
state AutomaticDoor of
    timeToClose : Time
init s == s = mk_AutomaticDoor(<TWO>)
end
operations
    tick : () ==> ()
    tick() ==
        (Motor`tick();
        if Sensor`detect()
            then (timeToClose := OPEN_DURATION;
            Motor`open())
        elseif Motor`isOpen()
            then cases timeToClose:
                <TWO> -> timeToClose := <ONE>,
                <ONE> -> timeToClose := <ZERO>,
                <ZERO> -> Motor`close()
                end)
    post 
        (Sensor`detect()
            => not Motor`isClosing() and timeToClose = OPEN_DURATION)
        and (not Sensor`detect() and Motor`isOpen()
            => timeToClose <> OPEN_DURATION);
traces
    TICK_ONLY:
        tick() *;
    
    TICK_SENSOR_TICK_SENSOR_TICK:
        (tick() *;
        (Sensor`setSensor(true) |
        Sensor`setSensor(false));
        tick() *;
        (Sensor`setSensor(true) |
        Sensor`setSensor(false));
        tick() *);
end AutomaticDoor