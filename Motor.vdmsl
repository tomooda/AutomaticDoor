module Motor
exports all
definitions
types
    Command = <open> | <close> | <halt>;
    
    Position = real inv p == 0.0 <= p and p <= 1.0;
values
    SPEED : real = 0.2;
    
    OPEN_POSITION : Position = 1.0;
    
    CLOSED_POSITION : Position = 0.0;
state Motor of
    command : Command
    position : Position
init s == s = mk_Motor(<halt>, OPEN_POSITION)
end
operations
    open : () ==> ()
    open() == command := <open>;
    
    close : () ==> ()
    close() == command := <close>;
    
    halt : () ==> ()
    halt() == command := <halt>;
    
    pure getPosition : () ==> Position
    getPosition() == return position;
    
    setPosition : Position ==> ()
    setPosition(p) == position := p;
    
    pure isOpening : () ==> bool
    isOpening() == return command = <open>;
    
    pure isClosing : () ==> bool
    isClosing() == return command = <close>;
    
    pure isHalted : () ==> bool
    isHalted() == return command = <halt>;
    
    pure isOpen : () ==> bool
    isOpen() == return position = OPEN_POSITION;
    
    pure isClosed : () ==> bool
    isClosed() == return position = CLOSED_POSITION;
    
    tick : () ==> ()
    tick() ==
        (cases command:
            <open> ->
                (position := min(position + SPEED, OPEN_POSITION);
                if isOpen() then halt()),
            <close> ->
                (position := max(position - SPEED, CLOSED_POSITION);
                if isClosed() then halt()),
            others -> skip
            end);
functions
    min : real * real -> real
    min(x, y) == if x <= y then x else y;
    
    max : real * real -> real
    max(x, y) == if x >= y then x else y;
end Motor